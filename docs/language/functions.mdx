---
title: Functions
sidebar_position: 7
---

Functions are sequences of statements that perform a specific task. Functions have parameters (inputs) and an optional return value (output). Functions are typed: the function type consists of the parameter types and the return type.

Functions are values (i.e., they can be assigned to constants and variables) and can be passed as arguments to other functions. This behavior is often called _first-class functions_.

## Function declarations

Functions can be declared by using the `fun` keyword, followed by the name of the declaration, the parameters, the optional return type, and the code that should be executed when the function is called.

The parameters must be enclosed in parentheses. The return type, if any, is separated from the parameters by a colon (`:`). The function code must be enclosed in opening and closing braces.

- Each parameter must have a name, which is the name that the argument value will be available as within the function.
- An additional argument label can be provided to require function calls to use the label to provide an argument value for the parameter.
- Argument labels make code more explicit and readable. For example, they avoid confusion about the order of arguments when there are multiple arguments that have the same type.
- Argument labels should be named so they make sense from the perspective of the function call.
- Argument labels precede the parameter name. The special argument label `_` indicates that a function call can omit the argument label. If no argument label is declared in the function declaration, the parameter name is the argument label of the function declaration, and function calls must use the parameter name as the argument label.

Each parameter must have a type annotation, which follows the parameter name after a colon.

Function calls may provide arguments for parameters, which are subtypes of the parameter types.

There is **no** support for optional parameters (i.e., default values for parameters and variadic functions, which are functions that take an arbitrary amount of arguments).

### Examples and explanations

Declare a function named `double`, which multiples a number by two:

```cadence
// The special argument label _ is specified for the parameter,
// so no argument label has to be provided in a function call.
//
fun double(_ x: Int): Int {
    return x * 2
}
```

Call the function named `double` with the value 4 for the first parameter:

```cadence
// The argument label can be omitted in the function call as the declaration
// specifies the special argument label _ for the parameter.
//
double(2)  // is `4`
```

It is possible to require argument labels for some parameters and not require argument labels for other parameters.

Declare a function named `clamp`. The function takes an integer value, the lower limit, and the upper limit. It returns an integer between the lower and upper limit:

```cadence
// For the first parameter, the special argument label _ is used,
// so no argument label has to be given for it in a function call.
//
// For the second and third parameter, no argument label is given,
// so the parameter names are the argument labels, i.e., the parameter names
// have to be given as argument labels in a function call.
//
fun clamp(_ value: Int, min: Int, max: Int): Int {
    if value > max {
        return max
    }

    if value < min {
        return min
    }

    return value
}
```

Declare a constant, which has the result of a call to the function named `clamp` as its initial value:

```cadence
// For the first argument, no label is given, as it is not required by
// the function declaration (the special argument label `_` is specified).
//
// For the second and this argument the labels must be provided,
// as the function declaration does not specify the special argument label `_`
// for these two parameters.
//
// As the function declaration also does not specify argument labels
// for these parameters, the parameter names must be used as argument labels.
//
let clamped = clamp(123, min: 0, max: 100)
// `clamped` is `100`
```

Declare a function named `send`, which transfers an amount from one account to another:

```cadence
// The implementation is omitted for brevity.
//
// The first two parameters of the function have the same type, so there is
// a potential that a function call accidentally provides arguments in
// the wrong order.
//
// While the parameter names `senderAddress` and `receiverAddress`
// are descriptive inside the function, they might be too verbose
// to require them as argument labels in function calls.
//
// For this reason the shorter argument labels `from` and `to` are specified,
// which still convey the meaning of the two parameters without being overly
// verbose.
//
// The name of the third parameter, `amount`, is both meaningful inside
// the function and also in a function call, so no argument label is given,
// and the parameter name is required as the argument label in a function call.
//
fun send(from senderAddress: Address, to receivingAddress: Address, amount: Int) {
    // The function code is omitted for brevity.
    // ...
}
```

Declare a constant that refers to the sending account's address:

```cadence
// The initial value is omitted for brevity.
//
let sender: Address = // ...
```

Declare a constant that refers to the receiving account's address:

```cadence
// The initial value is omitted for brevity.
//
let receiver: Address = // ...
```

Call the function named `send`:

```cadence
// The function declaration requires argument labels for all parameters,
// so they need to be provided in the function call.
//
// This avoids ambiguity. For example, in some languages (like C) it is
// a convention to order the parameters so that the receiver occurs first,
// followed by the sender. In other languages, it is common to have
// the sender be the first parameter, followed by the receiver.
//
// Here, the order is clear: send an amount from one account to another account.
//
send(from: sender, to: receiver, amount: 100)
```

The order of the arguments in a function call must match the order of the parameters in the function declaration.

Declare a function named `test`, which accepts two parameters named `first` and `second`:

```cadence
fun test(first: Int, second: Int) {
    // ...
}

// Invalid: the arguments are provided in the wrong order,
// even though the argument labels are provided correctly.
//
test(second: 1, first: 2)
```

Functions can be nested (i.e., the code of a function may declare further functions).

Declare a function that multiplies a number by two and adds one:

```cadence
fun doubleAndAddOne(_ x: Int): Int {

    // Declare a nested function that multiplies a number by two.
    //
    fun double(_ x: Int) {
        return x * 2
    }

    return double(x) + 1
}

doubleAndAddOne(2)  // is `5`
```

Functions do not support overloading.

## Function expressions

Functions can be also used as expressions. The syntax is the same as for function declarations, except that function expressions have no name (i.e., they are anonymous).

Declare a constant named `double`, which has a function as its value:

```cadence
// The function multiplies a number by two when it is called.
//
// This function's type is `fun (Int): Int`.
//
let double =
    fun (_ x: Int): Int {
        return x * 2
    }
```

## Function calls

Functions can be called (invoked). Function calls must provide exactly as many argument values as the function has parameters:

```cadence
fun double(_ x: Int): Int {
    return x * 2
}

// Valid: the correct number of arguments is provided.
//
double(2)  // is `4`

// Invalid: too many arguments are provided.
//
double(2, 3)

// Invalid: too few arguments are provided.
//
double()
```

## Function types

Function types consist of the `fun` keyword, the function's parameter types, and the function's return type.

The parameter types must be enclosed in parentheses, followed by a colon (`:`), and end with the return type.

Optionally, the `view` keyword can be included before the `fun` keyword to indicate that the type is that of a `view` function.

Declare a function named `add` with the function type `fun(Int, Int): Int`:

```cadence
fun add(a: Int, b: Int): Int {
    return a + b
}
```

Declare a constant named `add` with the function type `fun(Int, Int): Int`:

```cadence
let add: fun(Int, Int): Int =
    fun (a: Int, b: Int): Int {
        return a + b
    }
```

If the function has no return type, it implicitly has the return type `Void`.

Declare a constant named `add` with the function type `fun(Int, Int): Int`:

```cadence
let doNothing: fun(): Void =
    fun () {}
```

Parentheses also control precedence. For example, a function type `fun(Int): fun(): Int` is the type for a function that accepts one argument with type `Int`, and which returns another function, that takes no arguments and returns an `Int`.

The type `[fun(Int): Int; 2]` specifies an array type of two functions, which accept one integer and return one integer.

Argument labels are not part of the function type. This has the advantage that functions with different argument labels, potentially written by different authors, are compatible as long as the parameter types and the return type match. It has the disadvantage that function calls to plain function values cannot accept argument labels.

Declare a function, which takes one argument that has the type `Int`:

```cadence
// The function has type `fun(Int): Void`.
//
fun foo1(x: Int) {}

// Call function `foo1`. This requires an argument label.
foo1(x: 1)
```

Declare another function, which takes one argument that has the type `Int`:

```cadence
// The function also has type `fun(Int): Void`.
//
fun foo2(y: Int) {}
```

Call function `foo2`, which requires an argument label:

```cadence
foo2(y: 2)
```

Declare a variable, which has type `fun(Int): Void`, and use `foo1` as its initial value:

```cadence
var someFoo: fun(Int): Void = foo1
```

Call the function assigned to variable `someFoo`:

```cadence
// This is valid as the function types match.
// This does neither require nor allow argument labels.
//
someFoo(3)
```

Assign function `foo2` to variable `someFoo`:

```cadence
// This is valid as the function types match.
//
someFoo = foo2
```

Call the function assigned to variable `someFoo`:

```cadence
// This does neither require nor allow argument labels.
//
someFoo(4)
```

## Closures

A function may refer to variables and constants of its outer scopes in which it is defined. It is called a closure because it is closing over those variables and constants. A closure can read from the variables and constants and assign to the variables it refers to.

Declare a function named `makeCounter`, which returns a function that, each time when called, returns the next integer, starting at 1:

```cadence
fun makeCounter(): fun(): Int {
    var count = 0
    return fun (): Int {
        // NOTE: read from and assign to the non-local variable
        // `count`, which is declared in the outer function.
        //
        count = count + 1
        return count
    }
}

let test = makeCounter()
test()  // is `1`
test()  // is `2`
```

## Argument passing behavior

When arguments are passed to a function, they are copied. Therefore, values that are passed into a function are unchanged in the caller's scope when the function returns. This behavior is known as [_call by value_].

Declare a function that changes the first two elements of an array of integers:

```cadence
fun change(_ numbers: [Int]) {
    // Change the elements of the passed-in array.
    // The changes are only local, as the array was copied.
    //
    numbers[0] = 1
    numbers[1] = 2
    // `numbers` is `[1, 2]`
}

let numbers = [0, 1]

change(numbers)
// `numbers` is still `[0, 1]`
```

Parameters are constant (i.e., it is not allowed to assign to them):

```cadence
fun test(x: Int) {
    // Invalid: cannot assign to a parameter (constant)
    //
    x = 2
}
```

## Function pre-conditions and post-conditions

Functions may have pre-conditions and may have post-conditions:

```cadence
fun factorial(_ n: Int): Int {
    pre {
        // Require the parameter `n` to be greater than or equal to zero.
        //
        n >= 0:
            "factorial is only defined for integers greater than or equal to zero"
    }
    post {
        // Ensure the result will be greater than or equal to 1.
        //
        result >= 1:
            "the result must be greater than or equal to 1"
    }

    if n < 1 {
       return 1
    }

    return n * factorial(n - 1)
}

factorial(5)  // is `120`

// Run-time error: The given argument does not satisfy
// the precondition `n >= 0` of the function, the program aborts.
//
factorial(-2)
```

See [pre- and post-conditions] for more information.

## View functions

Functions can be annotated as `view` to indicate that they do not modify any external state or any account state. A `view` annotation can be added to the beginning of a function declaration or expression like so: 

```cadence
access(all) 
view fun foo(): Void {}

let x = view fun(): Void {}

access(all) 
struct S {

    access(all)
    view fun foo(): Void {}
    
    view init()
}
```

All functions that do not have a `view` annotation are considered non-view and cannot be called inside of `view` contexts, such as inside a `view` function or in a precondition or postcondition. 

Function types can also have `view` annotations, which are placed after the opening parenthesis but before the parameter list. So, for example, these are valid types:

```cadence
let f: view fun (Int): Int = ...
let h: view fun (): (view fun (): Void) = ...
```

Any function types without a `view` annotation will be considered non-view. 

Functions are covariant with respect to `view` annotations, so a `view` function is a subtype of a non-view function with the same parameters and return types. Thus, the following declarations would typecheck:

```cadence
let a: view fun (): Void = view fun() {}
let b: fun (): Void = view fun() {}
let c: fun (): Void = fun() {}
let d: fun(view fun(): Void): Void = fun (x: fun(): Void) {} // contravariance
```

while these would not: 

```cadence
let x: view fun (): Void = fun() {}
let y: fun(fun(): Void): Void = fun(f: view fun(): Void) {} // contravariance
```

The operations that are not permitted in `view` contexts are: 

* Calling a non-view function (including any functions that modify account state or storage like `save` or `load`).
* Writing to or modifying any resources.
* Writing to or modifying any references.
* Indexed assignment or writes to any variables not statically knowable to have been defined in the current function's scope, or to any resources or references.

So, for example, this code would be allowed:

```cadence
view fun foo(): Int {
    let a: [Int] = []
    a[0] = 3
    return a.length
}
    ```

while this would not:

```cadence
let a: [Int] = []
view fun foo(): Int {
    a[0] = 3
    return a.length
}
```

A caveat to this is that in some cases a non-`view` function that only performs a mutation that would be allowed in a `view` context will be rejected as a limitation of the analysis. In particular, users may encounter this with arrays or dictionaries, where a function like: 

```cadence
view fun foo(): Int {
    let a: [Int] = [0]
    a[0] = 1
}
```

is acceptable, because `a` is local to this function, while 

```cadence
view fun foo(): Int {
    let a: [Int] = [0]
    a.append(1)
}
```

will be rejected, because `append` is not `view`. 

## Functions are values

Functions are values (_first-class_), so they may be assigned to variables and fields or passed to functions as arguments.

Declare a function named `transform`, which applies a function to each element of an array of integers and returns a new array of the results:

```cadence
access(all)
fun transform(function: fun(Int): Int, integers: [Int]): [Int] {
    var newIntegers: [Int] = []
    for integer in integers {
        newIntegers.append(function(integer))
    }
    return newIntegers
}

access(all)
fun double(_ integer: Int): Int {
    return integer * 2
}

let newIntegers = transform(function: double, integers: [1, 2, 3])
// `newIntegers` is `[2, 4, 6]`
```

<!-- Relative links. Will not render on the page -->

[_call by value_]: https://en.wikipedia.org/w/index.php?title=Evaluation_strategy&oldid=896280571#Call_by_value
[pre- and post-conditions]: ./pre-and-post-conditions.md