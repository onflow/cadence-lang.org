---
title: Cadence 1.0 Improvements & New Features
sidebar_position: 0
sidebar_label: Improvements & New Features
---

## ğŸ’« New features

<details>

<summary>View Functions added ([FLIP 1056](https://github.com/onflow/flips/blob/main/cadence/20220715-cadence-purity-analysis.md))</summary>

#### ğŸ’¡ Motivation

View functions enable developers to enhance the reliability and safety of their programs, facilitating a clearer understanding of the impacts of their own code and that of others.

Developers can mark their functions asÂ `view`, which disallows the function from performing state changes. That also makes the intent of functions clear to other programmers, as it allows them to distinguish between functions that change state and ones that do not.

#### â„¹ï¸ Description

Cadence has added support for annotating functions with theÂ `view`Â keyword, which enforces that no â€œmutatingâ€ operations occur inside the body of the function. TheÂ `view`Â keyword is placed before theÂ `fun`Â keyword in a function declaration or function expression.

If a function has noÂ `view`Â annotation, it is considered â€œnon-viewâ€, and users should encounter no difference in behavior in these functions from what they are used to.

If a function does have aÂ `view`Â annotation, then the following mutating operations are not allowed:

- Writing to, modifying, or destroying any resources
- Writing to or modifying any references
- Assigning to or modifying any variables that cannot be determined to have been created locally inside of theÂ `view`Â function in question. In particular, this means that captured and global variables cannot be written in these functions
- Calling a non-`view`Â function

This feature was proposed inÂ [FLIP 1056](https://github.com/onflow/flips/blob/main/cadence/20220715-cadence-purity-analysis.md). To learn more, please consult the FLIP and documentation.

#### ğŸ”„ Adoption

You can adopt view functions by adding theÂ `view`Â modifier to all functions that do not perform mutating operations.

#### âœ¨ Example

Before:
The functionÂ `getCount`Â of a hypothetical NFT collection returns the number of NFTs in the collection.

```cadence
access(all)
resource Collection {

  access(all)
  var ownedNFTs: @{UInt64: NonFungibleToken.NFT}

  init () {
    self.ownedNFTs <- {}
  }

  access(all)
  fun getCount(): Int {
    returnself.ownedNFTs.length
  }

  /* ... rest of implementation ... */
}
```

After:
The functionÂ `getCount`Â does not perform any state changes, it only reads the length of the collection and returns it. Therefore it can be marked asÂ `view.`

```cadence
    access(all)
    view fun getCount(): Int {
//  ^^^^ addedreturnself.ownedNFTs.length
    }
```

</details>

<details>

<summary>Interface Inheritance Added ([FLIP 40](https://github.com/onflow/flips/blob/main/cadence/20221024-interface-inheritance.md))</summary>

#### ğŸ’¡ Motivation

Previously, interfaces could not inherit from other interfaces, which required developers to repeat code.
Interface inheritance allows code abstraction and code reuse.

#### â„¹ï¸Â Description andÂ âœ¨Â Example

Interfaces can now inherit from other interfaces of the same kind. This makes it easier for developers to structure their conformances and reduces a lot of redundant code.

For example, suppose there are two resource interfacesÂ `Receiver`Â andÂ `Vault`, and suppose all implementations of theÂ `Vault`Â would also need to conform to the interfaceÂ `Receiver`.

Previously, there was no way to enforce this. Anyone who implements theÂ `Vault`Â would have to explicitly specify that their concrete type also implements theÂ `Receiver`. But it was not always guaranteed that all implementations would follow this informal agreement.
With interface inheritance, theÂ `Vault`Â interface can now inherit/conform to theÂ `Receiver`Â interface.

```cadence
access(all)
resource interface Receiver {
  access(all)
  fun deposit(_ something:@AnyResource)
}

access(all)
resource interface Vault: Receiver {
  access(all)
  fun withdraw(_ amount: Int):@Vault
}
```

Thus, anyone implementing theÂ `Vault`Â interface would also have to implement theÂ `Receiver`Â interface as well.

```cadence
access(all)
resource MyVault: Vault {
  // Required!
  access(all)
  fun withdraw(_ amount: Int):@Vault {}
  // Required!
  access(all)
  fun deposit(_ something:@AnyResource) {}
}
```

This feature was proposed inÂ [FLIP 40](https://github.com/onflow/flips/blob/main/cadence/20221024-interface-inheritance.md). To learn more, please consult the FLIP and documentation.

</details>

## âš¡ Breaking Improvements

Many of the improvements of Cadence 1.0 are fundamentally changing how Cadence works and how it is used. However, that also means it is necessary to break existing code to release this version, which will guarantee stability (no more planned breaking changes) going forward.

Once Cadence 1.0 is live, breaking changes will simply not be acceptable.

So we have, and need to use, this last chance to fix and improve Cadence, so it can deliver on its promise of being a language that provides security and safety, while also providing composability and simplicity.

We fully recognize the frustration developers feel when updates break their code, necessitating revisions. Nonetheless, we are convinced that this inconvenience is justified by the substantial enhancements to Cadence development. These improvements not only make development more effective and enjoyable but also empower developers to write and deploy immutable contracts.

The improvements were intentionally bundled into one release to avoid breaking Cadence programs multiple times.

<details>

<summary> **2024-04-24** Public Capability Acquisition No Longer Returns Optional Capabilities ([FLIP 242](https://github.com/onflow/flips/blob/main/cadence/20240123-capcon-get-capability-api-improvement.md))</summary>

**Note** This is a recent change that may not be reflected in emulated migrations or all tools yet.  Likewise, this may affect existing staged contracts which do not conform to this new requirement.  Please ensure your contracts are updated and re-staged, if necessary, to match this new requirement.

#### ğŸ’¡ Motivation

In the initial implementation of the new Capability Controller API (a change that is new in Cadence 1.0, proposed in [FLIP 798](https://github.com/onflow/flips/blob/main/cadence/20220203-capability-controllers.md)), `capabilities.get<T>` would return an optional capability, `Capability<T>?`.  When the no capability was published under the requested path, or when type argument `T` was not a subtype of the runtime type of the capability published under the requested path, the capability would be `nil`.

This was a source of confusion among developers, as previously `account.getCapability<T>` did not return an optional capability, but rather one that would simply fail `capability.borrow` if the capability was invalid.

It was concluded that this new behaviour was not ideal, and that there a benefit to an invalid Capability not being `nil`, even if it is not borrowable. A `nil` capability lacked information that was previously available with an invalid capability - primarily the type and address of the capability.  Developers may have wanted to make use of this information, and react to the capability being invalid, as opposed to an uninformative `nil` value and encountering a panic scenario.

#### â„¹ï¸ Description

The `capabilities.get<T>` function now returns an invalid capability when no capability is published under the requested path, or when the type argument `T` is not a subtype of the runtime type of the capability published under the requested path.

This capability has the following properties:

- Always return `false` when `Capability<T>.check` is called.
- Always return `nil` when `Capability<T>.borrow` is called.
- Have an ID of `0`.
- Have a runtime type that is the same as the type requested in the type argument of `capabilities.get<T>`.
<br />

#### ğŸ”„ Adoption

If you have not updated your code to Cadence 1.0 yet, you will need to follow the same guidelines for updating to the Capability Controller API as you would have before, but will need to handle the new invalid capability type instead of an optional capability.

If you have already updated your code to use `capabilities.get<T>`, and are handling the capability as an optional type, you may need to update your code to handle the new non-optional invalid capability type instead.

#### âœ¨ Example

**Before:**

```cadence
let capability = account.capabilities.get<&MyNFT.Collection>(/public/NFTCollection)
if capability == nil {
    // Handle the case where the capability is nil
}
```

**After:**

```cadence
let capability = account.capabilities.get<&MyNFT.Collection>(/public/NFTCollection)
if !capability.check() {
    // Handle the case where the capability is invalid
}
```

</details>

<details>

<summary>**2024-04-23** Matching Access Modifiers for Interface Implementation Members are now Required ([FLIP 262](https://github.com/onflow/flips/blob/main/cadence/20240415-remove-non-public-entitled-interface-members.md))</summary>

**Note** This is a recent change that may not be reflected in emulated migrations or all tools yet.  Likewise, this may affect existing staged contracts which do not conform to this new requirement.  Please ensure your contracts are updated and re-staged, if necessary, to match this new requirement.

#### ğŸ’¡ Motivation

Previously, the access modifier of a member in a type conforming to / implementing an interface
could not be more restrictive than the access modifier of the member in the interface.
That meant an implementation may have choosen to use a more permissive access modifier than the interface.

This may have been surprising to developers, as they may have assumed that the access modifier of the member
in the interface was a _requirement_ / _maximum_, not just a minimum, especially when using
a non-public / non-entitled access modifier (e.g. `access(contract)`, `access(account)`).

Requiring access modifiers of members in the implementation to match the access modifiers
of members given in the interface, helps avoid confusion and potential footguns.

#### â„¹ï¸ Description

If an interface member has an access modifier, a composite type that conforms to it / implements
the interface must use exactly the same access modifier.

#### ğŸ”„ Adoption

Update the access modifiers of members in composite types that conform to / implement interfaces if they do not match the access modifiers of the members in the interface.

#### âœ¨ Example

**Before:**

```cadence
access(all)
resource interface I {
  access(account)
  fun foo()
}

access(all)
resource R: I {
  access(all)
  fun foo() {}
}
```

**After:**

```cadence
access(all)
resource interface I {
  access(account)
  fun foo()
}

access(all)
resource R: I {
  access(account)
  fun foo() {}
}
```

</details>

<details>

<summary>Conditions No Longer Allow State Changes ([FLIP 1056](https://github.com/onflow/flips/blob/main/cadence/20220715-cadence-purity-analysis.md))</summary>

#### ğŸ’¡ Motivation

In the current version of Cadence, pre-conditions and post-conditions may perform state changes, e.g. by calling a function that performs a mutation. This may result in unexpected behavior, which might lead to bugs.

To make conditions predictable, they are no longer allowed to perform state changes.

#### â„¹ï¸ Description

Pre-conditions and post-conditions are now consideredÂ `view`Â contexts, meaning that any operations that would be prevented inside of aÂ `view`Â function are also not permitted in a pre-condition or post-condition.

This is to prevent underhanded code wherein a user modifies global or contract state inside of a condition, where they are meant to simply be asserting properties of that state.

In particular, since only expressions were permitted inside conditions already, this means that if users wish to call any functions in conditions, these functions must now be madeÂ `view`Â functions.

This improvement was proposed inÂ [FLIP 1056](https://github.com/onflow/flips/blob/main/cadence/20220715-cadence-purity-analysis.md). To learn more, please consult the FLIP and documentation.

#### ğŸ”„ Adoption

Conditions which perform mutations will now result in the error â€œImpure operation performed in view contextâ€.
Adjust the code in the condition so it does not perform mutations.

The condition may be considered mutating, because it calls a mutating, i.e. non-`view`Â function. It might be possible to mark the called function asÂ `view`, and the body of the function may need to get updated in turn.

#### âœ¨ Example

**Before:**

The functionÂ `withdraw`Â of a hypothetical NFT collection interface allows the withdrawal of an NFT with a specific ID. In its post-condition, the function states that at the end of the function, the collection should have exactly one fewer item than at the beginning of the function.

```cadence
access(all)
resource interface Collection {

  access(all)
  fun getCount(): Int

  access(all)
  fun withdraw(id: UInt64):@NFT {
    post {
      getCount() == before(getCount()) - 1
    }
  }

  /* ... rest of interface ... */
}
```

**After:**

The calls toÂ `getCount`Â in the post-condition are not allowed and result in the error â€œImpure operation performed in view contextâ€, because theÂ `getCount`Â function is considered a mutating function, as it does not have theÂ `view`Â modifier.

Here, as theÂ `getCount`Â function only performs a read-only operation and does not change any state, it can be marked asÂ `view`.

```cadence
    access(all)
    view fun getCount(): Int
//  ^^^^
```

</details>

<details>

<summary>Missing or Incorrect Argument Labels Get Reported</summary>

#### ğŸ’¡ Motivation

Previously, missing or incorrect argument labels of function calls were not reported. This had the potential to confuse developers or readers of programs, and could potentially lead to bugs.

#### â„¹ï¸ Description

Function calls with missing argument labels are now reported with the error message â€œmissing argument labelâ€, and function calls with incorrect argument labels are now reported with the error message â€œincorrect argument labelâ€.

#### ğŸ”„ Adoption

- Function calls with missing argument labels should be updated to include the required argument labels.
- Function calls with incorrect argument labels should be fixed by providing the correct argument labels.

#### âœ¨ Example

ContractÂ `TestContract`Â deployed at addressÂ `0x1`:

```cadence
access(all)
contract TestContract {

  access(all)
  structTestStruct {

  access(all)
  let a: Int

  access(all)
  let b: String

  init(first: Int, second: String) {
    self.a = first
    self.b = second
    }
  }
}
```

**Incorrect program**:

The initializer ofÂ `TestContract.TestStruct`Â expects the argument labelsÂ `first`Â andÂ `second`.

However, the call of the initializer provides the incorrect argument labelÂ `wrong`Â for the first argument, and is missing the label for the second argument.

```cadence
// Script
import TestContract from 0x1

access(all)
fun main() {
  TestContract.TestStruct(wrong: 123, "abc")
}
```

This now results in the following errors:

```
error: incorrect argument label
  --> script:4:34
   |
 4 |           TestContract.TestStruct(wrong: 123, "abc")
   |                                   ^^^^^ expected `first`, got `wrong`

error: missing argument label: `second`
  --> script:4:46
   |
 4 |           TestContract.TestStruct(wrong: 123, "abc")
   |                                               ^^^^^
```

**Corrected program**:

```cadence
// Script
import TestContract from 0x1

access(all)
fun main() {
  TestContract.TestStruct(first: 123, second: "abc")
}
```

We would like to thank community member @justjoolz for reporting this bug.

</details>

<details>

<summary>Incorrect Operators In Reference Expressions Get Reported ([FLIP 941](https://github.com/onflow/flips/blob/main/cadence/20220516-reference-creation-semantics.md))</summary>

#### ğŸ’¡ Motivation

Previously, incorrect operators in reference expressions were not reported.

This had the potential to confuse developers or readers of programs, and could potentially lead to bugs.

#### â„¹ï¸ Description

The syntax for reference expressions isÂ `&v as &T`, which represents taking a reference to valueÂ `v`Â as typeÂ `T`.
Reference expressions that used other operators, such asÂ `as?`Â andÂ `as!`, e.g.Â `&v as! &T`, were incorrect and were previously not reported as an error.

The syntax for reference expressions improved to justÂ `&v`. The type of the resulting reference must still be provided explicitly.
If the type is not explicitly provided, the error â€œcannot infer type from reference expression: requires an explicit type annotationâ€ is reported.

For example, existing expressions likeÂ `&v as &T`Â provide an explicit type, as they statically assert the type usingÂ `as &T`. Such expressions thus keep working and doÂ *not*Â have to be changed.

Another way to provide the type for the reference is by explicitly typing the target of the expression, for example, in a variable declaration, e.g. viaÂ `let ref: &T = &v`.

This improvement was proposed inÂ [FLIP 941](https://github.com/onflow/flips/blob/main/cadence/20220516-reference-creation-semantics.md). To learn more, please consult the FLIP and documentation.

#### ğŸ”„ Adoption

Reference expressions which use an operator other thanÂ `as`Â need to be changed to use theÂ `as`Â operator.
In cases where the type is already explicit, the static type assertion (`as &T`) can be removed.

#### âœ¨ Example

**IncorrectÂ program**:
The reference expression uses the incorrect operatorÂ `as!`.

```cadence
let number = 1
let ref = &number as! &Int
```

This now results in the following error:

```bash
error: cannot infer type from reference expression: requires an explicit type annotation
 --> test:3:17
  |
3 |let ref = &number as! &Int
  |           ^
```

**CorrectedÂ program**:

```cadence
let number = 1
let ref = &number as &Int
```

Alternatively, the same code can now also be written as follows:

```cadence
let number = 1
let ref: &Int = &number
```

</details>

<details>

<summary>Tightening Of Naming Rules</summary>

#### ğŸ’¡ Motivation

Previously, Cadence allowed language keywords (e.g.Â `continue`,Â `for`, etc.) to be used as names. For example, the following program was allowed:

```cadence
fun continue(import: Int, break: String) { ... }
```

This had the potential to confuse developers or readers of programs, and could potentially lead to bugs.

#### â„¹ï¸ Description

Most language keywords are no longer allowed to be used as names.
Some keywords are still allowed to be used as names, as they have limited significance within the language. These allowed keywords are as follows:

- `from`: only used in import statementsÂ `import foo from ...`
- `account`: used in access modifiersÂ `access(account) let ...`
- `all`: used in access modifierÂ `access(all) let ...`
- `view`: used as modifier for function declarations and expressionsÂ `view fun foo()...`, letÂ `f = view fun () ...`
  Any other keywords will raise an error during parsing, such as:

```cadence
let break: Int = 0
//  ^ error: expected identifier after start of variable declaration, got keyword break
```

#### ğŸ”„ Adoption

Names which use language keywords must be renamed.

#### âœ¨ Example

**Before:**
A variable is named after a language keyword.

```cadence
let contract = signer.borrow<&MyContract>(name: "MyContract")
//  ^ error: expected identifier after start of variable declaration, got keyword contract
```

**After:**
The variable is renamed to avoid the clash with the language keyword.

```cadence
let myContract = signer.borrow<&MyContract>(name: "MyContract")
```

</details>

<details>

<summary>Result ofÂ `toBigEndianBytes()`Â forÂ `U?Int(128|256)`Â Fixed</summary>

#### ğŸ’¡ Motivation

Previously, the implementation ofÂ `.toBigEndianBytes()`Â was incorrect for the large integer typesÂ `Int128`,Â `Int256`,Â `UInt128`, andÂ `UInt256`.

This had the potential to confuse developers or readers of programs, and could potentially lead to bugs.

#### â„¹ï¸ Description

Calling theÂ `toBigEndianBytes`Â function on smaller sized integer types returns the exact number of bytes that fit into the type, left-padded with zeros. For instance,Â `Int64(1).toBigEndianBytes()`Â returns an array of 8 bytes, as the size ofÂ `Int64`Â is 64 bits, 8 bytes.

Previously, theÂ `toBigEndianBytes`Â function erroneously returned variable-length byte arrays without padding for the large integer typesÂ `Int128`,Â `Int256`,Â `UInt128`, andÂ `UInt256`. This was inconsistent with the smaller fixed-size numeric types, such asÂ `Int8`, andÂ `Int32`.

To fix this inconsistency,Â `Int128`Â andÂ `UInt128`Â now always return arrays of 16 bytes, whileÂ `Int256`Â andÂ `UInt256`Â return 32 bytes.

#### âœ¨ Example

```cadence
let someNum: UInt128 = 123456789
let someBytes: [UInt8] = someNum.toBigEndianBytes()
// OLD behavior;
// someBytes = [7, 91, 205, 21]
// NEW behavior:
// someBytes = [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 7, 91, 205, 21]
```

#### ğŸ”„ Adoption

Programs that useÂ `toBigEndianBytes`Â directly, or indirectly by depending on other programs, should be checked for how the result of the function is used. It might be necessary to adjust the code to restore existing behavior.

If a program relied on the previous behavior of truncating the leading zeros, then the old behavior can be recovered by first converting to a variable-length type,Â `Int`Â orÂ `UInt`, as theÂ `toBigEndianBytes`Â function retains the variable-length byte representations, i.e. the result has no padding bytes.

```cadence
let someNum: UInt128 = 123456789
let someBytes: [UInt8] = UInt(someNum).toBigEndianBytes()
// someBytes = [7, 91, 205, 21]
```

</details>

<details>

<summary>Syntax for Function Types Improved ([FLIP 43](https://github.com/onflow/flips/blob/main/cadence/20221018-change-fun-type-syntax.md))</summary>

#### ğŸ’¡ Motivation

Previously, function types were expressed using a different syntax from function declarations or expressions. The previous syntax was unintuitive for developers, making it hard to write and read code that used function types.

#### â„¹ï¸ Description andÂ âœ¨Â examples

Function types are now expressed using theÂ `fun`Â keyword, just like expressions and declarations. This improves readability and makes function types more obvious.

For example, given the following function declaration:

```cadence
fun foo(n: Int8, s: String): Int16 { /* ... */ }
```

The functionÂ `foo`Â now has the typeÂ `fun(Int8, String): Int16`.
TheÂ `:`Â token is right-associative, so functions that return other functions can have their types written without nested parentheses:

```cadence
fun curriedAdd(_ x: Int): fun(Int): Int {
  return fun(_ y: Int): Int {
    return x+ y
  }
}
// function `curriedAdd` has the type `fun(Int): fun(Int): Int`
```

To further bring the syntax for function types closer to the syntax of function declarations expressions, it is now possible to omit the return type, in which case the return type defaults toÂ `Void`.

```cadence
fun logTwice(_ value: AnyStruct) {// Return type is implicitly `Void`
  log(value)
  log(value)
}

// The function types of these variables are equivalent
let logTwice1: fun(AnyStruct): Void = logTwice
let logTwice2: fun(AnyStruct) = logTwice
```

As a bonus consequence, it is now allowed for any type to be parenthesized. This is useful for complex type signatures, or for expressing optional functions:

```cadence
// A function that returns an optional Int16
let optFun1: fun (Int8): Int16? =
  fun (_: Int8): Int? { return nil }

// An optional function that returns an Int16
let optFun2: (fun (Int8): Int16)? = nil
```

This improvement was proposed in [FLIP 43](https://github.com/onflow/flips/blob/main/cadence/20221018-change-fun-type-syntax.md).

#### ğŸ”„ Adoption

Programs that use the old function type syntax need to be updated by replacing the surrounding parentheses of function types with theÂ `fun`Â keyword.

**Before:**

```cadence
let baz: ((Int8, String): Int16) = foo
      // ^                     ^
      // surrounding parentheses of function type
```

**After:**

```cadence
let baz: fun (Int8, String): Int16 = foo
```

</details>

<details>

<summary>Entitlements and Safe Down-casting ([FLIP 54](https://github.com/onflow/flips/blob/main/cadence/20221214-auth-remodel.md) & [FLIP 94](https://github.com/onflow/flips/blob/main/cadence/20230623-entitlement-improvements.md))</summary>

#### ğŸ’¡ Motivation

Previously, Cadenceâ€™s main access-control mechanism, restricted reference types, has been a source of confusion and mistakes for contract developers.

Developers new to Cadence often were surprised and did not understand why access-restricted functions, like theÂ `withdraw`Â function of the fungible tokenÂ `Vault`Â resource type, were declared asÂ `pub`, making the function publicly accessible â€“ access would later be restricted through a restricted type.

It was too easy to accidentally give out aÂ `Capability`Â with a more permissible type than intended, leading to security problems.
Additionally, because what fields and functions were available to a reference depended on what the type of the reference was, references could not be downcast, leading to ergonomic issues.

#### â„¹ï¸ Description

Access control has improved significantly.
When giving another user a reference orÂ `Capability`Â to a value you own, the fields and functions that the user can access is determined by the type of the reference orÂ `Capability`.

Previously, access to a value of typeÂ `T`, e.g. via a referenceÂ `&T`, would give access to all fields and functions ofÂ `T`. Access could be restricted, by using a restricted type. For example, a restricted referenceÂ `&T{I}`Â could only access members that wereÂ `pub`Â onÂ `I`. Since references could not be downcast, any members defined onÂ `T`Â but not onÂ `I`Â were unavailable to this reference, even if they wereÂ `pub`.

Access control is now handled using a new feature called Entitlements, as originally proposed acrossÂ [FLIP 54](https://github.com/onflow/flips/blob/main/cadence/20221214-auth-remodel.md) andÂ [FLIP 94](https://github.com/onflow/flips/blob/main/cadence/20230623-entitlement-improvements.md).

A reference can now be â€œentitledâ€ to certain facets of an object. For example, the referenceÂ `auth(Withdraw) &Vault`Â is entitled to access fields and functions ofÂ `Vault`Â which require theÂ `Withdraw`Â entitlement.

Entitlements can be are declared using the newÂ `entitlement`Â syntax.

Members can be made to require entitlements using the access modifier syntaxÂ `access(E)`, whereÂ `E`Â is an entitlement that the user must posses.

For example:

```cadence
entitlement Withdraw

access(Withdraw)
fun withdraw(amount: UFix64): @Vault
```

References can now always be down-casted, the standaloneÂ `auth`Â modifier is not necessary anymore, and got removed.

For example, the referenceÂ `&{Provider}`Â can now be downcast toÂ `&Vault`, so access control is now handled entirely through entitlements, rather than types.

To learn more, please refer to theÂ [documentation](https://cadence-lang.org/docs/1.0/language/access-control#entitlements).

#### ğŸ”„ Adoption

The access modifiers of fields and functions need to be carefully audited and updated.

Fields and functions that have theÂ `pub`Â access modifier are now callable by anyone with any reference to that type. If access to the member should be restricted, theÂ `pub`Â access modifier needs to be replaced with an entitlement access modifier.

When creating aÂ `Capability`Â or a reference to a value, **it must be carefully considered which entitlements are provided to the recipient of thatÂ `Capability`Â or reference** â€“ only the entitlements which are necessary and not more should be include in theÂ `auth`Â modifier of the reference type.

#### âœ¨ Example

**Before:**
TheÂ `Vault`Â resource was originally written like so:

```cadence
access(all)
resource interface Provider {
  access(all)
  funwithdraw(amount:UFix64): @Vault {
  // ...
  }
}

access(all)
resource Vault: Provider, Receiver, Balance {
  access(all)
  fun withdraw(amount:UFix64): @Vault {
  // ...
  }

  access(all)
  fun deposit(from: @Vault) {
  // ...
  }

  access(all)
  var balance: UFix64
}
```

**After:**
TheÂ `Vault`Â resource might now be written like this:

```cadence
access(all) entitlement Withdraw

access(all)
resource interface Provider {
  access(Withdraw)
  funwithdraw(amount:UFix64): @Vault {
  // ...
  }
}

access(all)
resource Vault: Provider, Receiver, Balance {

  access(Withdraw)// withdrawal requires permission
  fun withdraw(amount:UFix64): @Vault {
  // ...
  }

  access(all)
  fun deposit(from: @Vault) {
  // ...
  }

  access(all)
  var balance: UFix64
}
```

Here, theÂ `access(Withdraw)`Â syntax means that a reference toÂ `Vault`Â must possess theÂ `Withdraw`Â entitlement in order to be allowed to call theÂ `withdraw`Â function, which can be given when a reference orÂ `Capability`Â is created by using a new syntax:Â `auth(Withdraw) &Vault`.

This would allow developers to safely downcastÂ `&{Provider}`Â references toÂ `&Vault`Â references if they want to access functions likeÂ `deposit`Â andÂ `balance`, without enabling them to callÂ `withdraw`.

</details>

<details>

<summary>Removal of `pub`Â andÂ `priv`Â Access Modifiers ([FLIP 84](https://github.com/onflow/flips/blob/main/cadence/20230505-remove-priv-and-pub.md))</summary>

#### ğŸ’¡ Motivation

With the previously mentioned entitlements feature, which usesÂ `access(E)`Â syntax to denote entitled access, theÂ `pub`,Â `priv`Â andÂ `pub(set)`Â modifiers became the only access modifiers that did not use theÂ `access`Â syntax.

This made the syntax inconsistent, making it harder to read and understand programs.

In addition,Â `pub`Â andÂ `priv`Â already had alternatives/equivalents:Â `access(all)`Â andÂ `access(self)`.

#### â„¹ï¸ Description

TheÂ `pub`,Â `priv`Â andÂ `pub(set)`Â access modifiers are being removed from the language, in favor of their more explicitÂ `access(all)`Â andÂ `access(self)`Â equivalents (forÂ `pub`Â andÂ `priv`, respectively).

This makes access modifiers more uniform and better match the new entitlements syntax.

This improvement was originally proposed inÂ [FLIP 84](https://github.com/onflow/flips/blob/main/cadence/20230505-remove-priv-and-pub.md).

#### ğŸ”„ Adoption

Users should replace anyÂ `pub`Â modifiers withÂ `access(all)`, and anyÂ `priv`Â modifiers withÂ `access(self)`.

Fields that were defined asÂ `pub(set)`Â will no longer be publicly assignable, and no access modifier now exists that replicates this old behavior. If the field should stay publicly assignable, aÂ `access(all)`Â setter function that updates the field needs to be added, and users have to switch to using it instead of directly assigning to the field.

#### âœ¨ Example

**Before:**
Types and members could be declared withÂ `pub`Â andÂ `priv`:

```cadence
pub resource interface Collection {
  pub fun getCount(): Int

  priv fun myPrivateFunction()

  pub(set) let settableInt: Int

  /* ... rest of interface ... */
}
```

**After:**
The same behavior can be achieved withÂ `access(all)`Â andÂ `access(self)`

```cadence
access(all)
resource interface Collection {

  access(all)
  fun getCount(): Int

  access(self)
  fun myPrivateFunction()

  access(all)
  let settableInt: Int

  // Add a public setter method, replacing pub(set)
  access(all)
  fun setIntValue(_ i:Int): Int

  /* ... rest of interface ... */
}
```

</details>

<details>

<summary>Replacement of Restricted Types with Intersection Types ([FLIP 85](https://github.com/onflow/flips/blob/main/cadence/20230505-remove-restricted-types.md))</summary>

#### ğŸ’¡ Motivation

With the improvements to access control enabled by entitlements and safe down-casting, the restricted type feature is redundant.

#### â„¹ï¸ Description

Restricted types have been removed. All types, including references, can now be down-casted, restricted types are no longer used for access control.

At the same time intersection types got introduced. Intersection types have the syntaxÂ `{I1, I2, ... In}`, where all elements of the set of types (`I1, I2, ... In`) are interface types. A value is part of the intersection type if it conforms to all the interfaces in the intersection typeâ€™s interface set. This functionality is equivalent to restricted types that restrictedÂ `AnyStruct`Â andÂ `AnyResource.`

This improvement was proposed inÂ [FLIP 85](https://github.com/onflow/flips/blob/main/cadence/20230505-remove-restricted-types.md). To learn more, please consult the FLIP and documentation.

#### ğŸ”„ Adoption

Code that relies on the restriction behavior of restricted types can be safely changed to just use the concrete type directly, as entitlements will make this safe. For example,Â `&Vault{Balance}`Â can be replaced with justÂ `&Vault`, as access toÂ `&Vault`Â only provides access to safe operations, like getting the balance â€“ **privileged operations, like withdrawal, need additional entitlements.**

Code that usesÂ `AnyStruct`Â orÂ `AnyResource`Â explicitly as the restricted type, e.g. in a reference,Â `&AnyResource{I}`, needs to remove the use ofÂ `AnyStruct`Â /Â `AnyResource`. Code that already uses the syntaxÂ `&{I}`Â can stay as-is.

#### âœ¨ Example

**Before:**

This function accepted a reference to aÂ `T`Â value, but restricted what functions were allowed to be called on it to those defined on theÂ `X`,Â `Y`, andÂ `Z`Â interfaces.

```cadence
access(all)
resource interface X {
  access(all)
  fun foo()
}

access(all)
resource interface Y {
  access(all)
  fun bar()
}

access(all)
resource interface Z {
  access(all)
  fun baz()
}

access(all)
resource T: X, Y, Z {
  // implement interfaces
  access(all)
  fun qux() {
  // ...
  }
}

access(all)
fun exampleFun(param: &T{X, Y, Z}) {
  // `param` cannot call `qux` here, because it is restricted to
  // `X`, `Y` and `Z`.
}
```

**After:**
This function can be safely rewritten as:

```cadence
access(all)
resource interface X {
  access(all)
  fun foo()
}

access(all)
resource interface Y {
  access(all)
  fun bar()
}

resource interface Z {
  access(all)
  fun baz()
}

access(all)
entitlement Q

access(all)
resource T: X, Y, Z {
  // implement interfaces
  access(Q)
  fun qux() {
  // ...
  }
}

access(all)
fun exampleFun(param: &T) {
  // `param` still cannot call `qux` here, because it lacks entitlement `Q`
}
```

Any functions onÂ `T`Â that the author ofÂ `T`Â does not want users to be able to call publicly should be defined with entitlements, and thus will not be accessible to the unauthorizedÂ `param`Â reference, like withÂ `qux`Â above.

</details>

<details>

<summary>Account Access Got Improved ([FLIP 92](https://github.com/onflow/flips/blob/main/cadence/20230525-account-type.md))</summary>

#### ğŸ’¡ Motivation

Previously, access to accounts was granted wholesale: Users would sign a transaction, authorizing the code of the transaction to perform any kind of operation, for example, write to storage, but also add keys or contracts.

Users had to trust that a transaction would only perform supposed access, e.g. storage access to withdraw tokens, but still had to grant full access, which would allow the transaction to perform other operations.

Dapp developers who require users to sign transactions should be able to request the minimum amount of access to perform the intended operation, i.e. developers should be able to follow the principle of least privilege (PoLA).

This allows users to trust the transaction and Dapp.

#### â„¹ï¸ Description

Previously, access to accounts was provided through the built-in typesÂ `AuthAccount`Â andÂ `PublicAccount`:Â `AuthAccount`Â provided fullÂ *write*Â access to an account, whereasÂ `PublicAccount`Â only providedÂ *read*Â access.

With the introduction of entitlements, this access is now expressed using entitlements and references, and only a singleÂ `Account`Â type is necessary. In addition, storage related functionality were moved to the fieldÂ `Account.storage`.

Access to administrative account operations, such as writing to storage, adding keys, or adding contracts, is now gated by both coarse grained entitlements (e.g.Â `Storage`, which grants access to all storage related functions, andÂ `Keys`, which grants access to all key management functions), as well as fine-grained entitlements (e.g.Â `SaveValue`Â to save a value to storage, orÂ `AddKey`Â to add a new key to the account).

Transactions can now request the particular entitlements necessary to perform the operations in the transaction.

This improvement was proposed inÂ [FLIP 92](https://github.com/onflow/flips/blob/main/cadence/20230525-account-type.md). To learn more, consult the FLIP and the documentation.

#### ğŸ”„ Adoption

Code that previously usedÂ `PublicAccount`Â can simply be replaced with an unauthorized account reference,Â `&Account.`

Code that previously usedÂ `AuthAccount`Â must be replaced with an authorized account reference. Depending on what functionality of the account is accessed, the appropriate entitlements have to be specified.

For example, if theÂ `save`Â function ofÂ `AuthAccount`Â was used before, the function call must be replaced withÂ `storage.save`, and theÂ `SaveValue`Â orÂ `Storage`Â entitlement is required.

#### âœ¨ Example

**Before:**

The transactions wants to save a value to storage. It must request access to the whole account, even though it does not need access beyond writing to storage.

```cadence
transaction {
  prepare(signer: AuthAccount) {
    signer.save("Test", to: /storage/test)
  }
}
```

**After:**

The transaction requests the fine-grained account entitlementÂ `SaveValue`, which allows the transaction to call theÂ `save`Â function.

```cadence
transaction {
  prepare(signer: auth(SaveValue)&Account) {
    signer.storage.save("Test", to: /storage/test)
  }
}
```

If the transaction attempts to perform other operations, such as adding a new key, it is rejected:

```cadence
transaction {
  prepare(signer: auth(SaveValue)&Account) {
    signer.storage.save("Test", to: /storage/test)
    signer.keys.add(/* ... */)
    //          ^^^ Error: Cannot call function, requires `AddKey` or `Keys` entitlement
  }
}
```

</details>

<details>

<summary>Deprecated Key Management API Got Removed</summary>

#### ğŸ’¡ Motivation

Cadence provides two key management APIs:

- The original, low-level API, which worked with RLP-encoded keys
- The improved, high-level API, which works with convenient data types likeÂ `PublicKey`,Â `HashAlgorithm`, andÂ `SignatureAlgorithm`
  The improved API was introduced, as the original API was difficult to use and error-prone.
  The original API was deprecated in early 2022.

#### â„¹ï¸ Description

The original account key management API, got removed. Instead, the improved key management API should be used.
To learn more,

#### ğŸ”„ Adoption

Replace uses of the original account key management API functions with equivalents of the improved API:

| Removed | Replacement |
| --------------------------- | ------------------- |
| AuthAccount.addPublicKey | Account.keys.add |
| AuthAccount.removePublicKey | Account.keys.revoke |

To learn more, please refer to theÂ [documentation](https://developers.flow.com/cadence/language/accounts#account-keys).

#### âœ¨ Example

**Before:**

```cadence
transaction(encodedPublicKey: [UInt8]) {
  prepare(signer: AuthAccount) {
    signer.addPublicKey(encodedPublicKey)
  }
}
```

**After:**

```cadence
transaction(publicKey: [UInt8]) {
  prepare(signer: auth(Keys) &Account) {
    signer.keys.add(
      publicKey: PublicKey(
        publicKey: publicKey,
        signatureAlgorithm: SignatureAlgorithm.ECDSA_P256
      ),
      hashAlgorithm: HashAlgorithm.SHA3_256,
      weight: 100.0
    )
  }
}
```

</details>

<details>

<summary>Resource Tracking for Optional Bindings Improved</summary>

#### ğŸ’¡ Motivation

Previously, resource tracking for optional bindings (â€if-let statementsâ€) was implemented incorrectly, leading to errors for valid code.
This required developers to add workarounds to their code.

#### â„¹ï¸ Description

Resource tracking for optional bindings (â€if-let statementsâ€) was fixed.

For example, the following program used to be invalid, reporting a resource loss error forÂ `optR`:

```cadence
resource R {}
fun asOpt(_ r: @R): @R? {
  return <-r
}

fun test() {
  let r <- create R()
  let optR <- asOpt(<-r)
  if let r2 <- optR {
      destroy r2
  }
}
```

This program is now considered valid.

#### ğŸ”„ Adoption

New programs do not need workarounds anymore, and can be written naturally.

Programs that previously resolved the incorrect resource loss error with a workaround, for example by invalidating the resource also in the else-branch or after the if-statement, are now invalid:

```cadence
fun test() {
  let r <- createR()
  let optR <-asOpt(<-r)
  if let r2 <- optR {
    destroy r2
  } else {
    destroy optR
    // unnecessary, but added to avoid error
  }
}
```

The unnecessary workaround needs to be removed.

</details>

<details>

<summary>Definite Return Analysis Got Improved</summary>

#### ğŸ’¡ Motivation

Definite return analysis determines if a function always exits, in all possible execution paths, e.g. through aÂ `return`Â statement, or by calling a function that never returns, likeÂ `panic`.

This analysis was incomplete and required developers to add workarounds to their code.

#### â„¹ï¸ Description

The definite return analysis got significantly improved.

This means that the following program is now accepted: both branches of the if-statement exit, one using aÂ `return`Â statement, the other using a function that never returns,Â `panic`:

```cadence
resource R {}

fun mint(id: UInt64):@R {
  if id > 100 {
    return <- create R()
  } else {
    panic("bad id")
  }
}
```

The program above was previously rejected with a â€œmissing return statementâ€ error â€“ even though we can convince ourselves that the function will exit in both branches of the if-statement, and that any code after the if-statement is unreachable, the type checker was not able to detect that â€“ it now does.

#### ğŸ”„ Adoption

New programs do not need workarounds anymore, and can be written naturally.
Programs that previously resolved the incorrect error with a workaround, for example by adding an additional exit at the end of the function, are now invalid:

```cadence
resource R {}

fun mint(id: UInt64):@R {
  if id > 100 {
    return <- create R()
  } else {
    panic("bad id")
  }

  // unnecessary, but added to avoid error
  panic("unreachable")
}
```

The improved type checker now detects and reports the unreachable code after the if-statement as an error:

```bash
error: unreachable statement
--> test.cdc:12:4
  |
12|  panic("unreachable")
  |  ^^^^^^^^^^^^^^^^^^^^
exit status 1
```

To make the code valid, simply remove the unreachable code.

</details>

<details>

<summary>Semantics for Variables in For-Loop Statements Got Improved ([FLIP 13](https://github.com/onflow/flips/blob/main/cadence/20221011-for-loop-semantics.md))</summary>

#### ğŸ’¡ Motivation

Previously, the iteration variable ofÂ `for-in`Â loops was re-assigned on each iteration.

Even though this is a common behavior in many programming languages, it is surprising behavior and a source of bugs.

The behavior was improved to the often assumed/expected behavior of a new iteration variable being introduced for each iteration, which reduces the likelihood for a bug.

#### â„¹ï¸ Description

The behavior ofÂ `for-in`Â loops improved, so that a new iteration variable is introduced for each iteration.

This change only affects few programs, as the behavior change is only noticeable if the program captures the iteration variable in a function value (closure).

This improvement was proposed inÂ [FLIP 13](https://github.com/onflow/flips/blob/main/cadence/20221011-for-loop-semantics.md). To learn more, consult the FLIP and documentation.

#### âœ¨ Example

Previously,Â `values`Â would result inÂ `[3, 3, 3]`, which might be surprising and unexpected. This is becauseÂ `x`Â wasÂ *reassigned*Â the current array element on each iteration, leading to each function inÂ `fs`Â returning the last element of the array.

```cadence
// Capture the values of the array [1, 2, 3]
let fs: [((): Int)] = []
for x in [1, 2, 3] {
  // Create a list of functions that return the array value
  fs.append(fun (): Int {
    return x
  })
}

// Evaluate each function and gather all array values
let values: [Int] = []
for f in fs {
  values.append(f())
}
```

</details>

<details>

<summary>References to Resource-Kinded Values Get Invalidated When the Referenced Values Are Moved ([FLIP 1043](https://github.com/onflow/flips/blob/main/cadence/20220708-resource-reference-invalidation.md))</summary>

#### ğŸ’¡ Motivation

Previously, when a reference is taken to a resource, that reference remains valid even if the resource was moved, for example when created and moved into an account, or moved from one account into another.

In other words, references to resources stayed alive forever. This could be a potential safety foot-gun, where one could gain/give/retain unintended access to resources through references.

#### â„¹ï¸ Description

References are now invalidated if the referenced resource is moved after the reference was taken. The reference is invalidated upon the first move, regardless of the origin and the destination.

This feature was proposed inÂ [FLIP 1043](https://github.com/onflow/flips/blob/main/cadence/20220708-resource-reference-invalidation.md). To learn more, please consult the FLIP and documentation.

#### âœ¨ Example

```cadence
// Create a resource.
let r <-createR()

// And take a reference.
let ref = &r as &R

// Then move the resource into an account.
account.save(<-r, to: /storage/r)

// Update the reference.
ref.id = 2

```

Old behavior:

```cadence

// This will also update the referenced resource in the account.
ref.id = 2

```

The above operation will now result in a static error.

```cadence

// Trying to update/access the reference will produce a static error:
//     "invalid reference: referenced resource may have been moved or destroyed"
ref.id = 2

```

However, not all scenarios can be detected statically. e.g:

```cadence
fun test(ref: &R) {
  ref.id = 2
}
```

In the above function, it is not possible to determine whether the resource to which the reference was taken has been moved or not. Therefore, such cases are checked at run-time, and a run-time error will occur if the resource has been moved.

#### ğŸ”„ Adoption

Review code that uses references to resources, and check for cases where the referenced resource is moved. Such code may now be reported as invalid, or result in the program being aborted with an error when a reference to a moved resource is de-referenced.

</details>

<details>

<summary>Capability Controller API Replaced Existing Linking-based Capability API ([FLIP 798](https://github.com/onflow/flips/blob/main/cadence/20220203-capability-controllers.md))</summary>

#### ğŸ’¡ Motivation

Cadence encourages a capability-based security model. Capabilities are themselves a new concept that most Cadence programmers need to understand.

The existing API for capabilities was centered around â€œlinksâ€ and â€œlinkingâ€, and the associated concepts of the public and private storage domains, led to capabilities being even confusing and awkward to use.
An better API is easier to understand and easier to work with.

#### â„¹ï¸ Description

The existing linking-based capability API has been replaced by a more powerful and easier to use API based on the notion of Capability Controllers. The new API makes the creation of new and the revocation of existing capabilities simpler.

This improvement was proposed inÂ [FLIP 798](https://github.com/onflow/flips/blob/main/cadence/20220203-capability-controllers.md). To learn more, consult the FLIP and the documentation.

#### ğŸ”„ Adoption

Existing uses of the linking-based capability API must be replaced with the new Capability Controller API.

| Removed                                   | Replacement                                                     |
| ----------------------------------------- | --------------------------------------------------------------- |
| `AuthAccount.link`, with private path       | `Account.capabilities.storage.issue`                             |
| `AuthAccount.link`, with public path        | `Account.capabilities.storage.issue` and `Account.capabilities.publish` |
| `AuthAccount.linkAccount`                   | `AuthAccount.capabilities.account.issue`                         |
| `AuthAccount.unlink`, with private path     | - Get capability controller: `Account.capabilities.storage/account.get` <br /> - Revoke controller: `Storage/AccountCapabilityController.delete` |
| `AuthAccount.unlink`, with public path      | - Get capability controller: `Account.capabilities.storage/account.get` <br /> - Revoke controller: `Storage/AccountCapabilityController.delete` <br /> - Unpublish capability: `Account.capabilities.unpublish` |
| `AuthAccount/PublicAccount.getCapability`   | `Account.capabilities.get`                                        |
| `AuthAccount/PublicAccount.getCapability` with followed borrow | `Account.capabilities.borrow`                            |
| `AuthAccount.getLinkTarget`                 | N/A                                                             |


#### âœ¨ Example

Assume there is aÂ `Counter`Â resource which stores a count, and it implements an interfaceÂ `HasCount`Â which is used to allow read access to the count.

```cadence
access(all)
resource interface HasCount {
  access(all)
  count: Int
}

access(all)
resource Counter: HasCount {
  access(all)
  var count: Int

  init(count: Int) {
    self.count = count
  }
}
```

Granting access, before:

```cadence
transaction {
  prepare(signer: AuthAccount) {
    signer.save(
      <-create Counter(count: 42),
      to: /storage/counter
    )
    signer.link<&{HasCount}>(
      /public/hasCount,
      target: /storage/counter
    )
  }
}
```

Granting access, after:

```cadence
transaction {
  prepare(signer: auth(Storage, Capabilities)&Account) {
    signer.save(
      <-create Counter(count: 42),
      to: /storage/counter
    )
    let cap = signer.capabilities.storage.issue<&{HasCount}>(
      /storage/counter
    )
    signer.capabilities.publish(cap, at: /public/hasCount)
  }
}
```

Getting access, before:

```cadence
access(all)
fun main(): Int {
  let counterRef = getAccount(0x1)
    .getCapabilities<&{HasCount}>(/public/hasCount)
    .borrow()!
  return counterRef.count
}
```

Getting access, after:

```cadence
access(all)
fun main(): Int {
  let counterRef = getAccount(0x1)
    .capabilities
    .borrow<&{HasCount}>(/public/hasCount)!
  return counterRef.count
}
```

</details>

<details>

<summary>External Mutation Improvement ([FLIP 89](https://github.com/onflow/flips/blob/main/cadence/20230517-member-access-semnatics.md) & [FLIP 86](https://github.com/onflow/flips/blob/main/cadence/20230519-built-in-mutability-entitlements.md))</summary>

#### ğŸ’¡ Motivation

A previous version of Cadence (â€œSecure Cadenceâ€), attempted to prevent a common safety foot-gun: Developers might use theÂ `let`Â keyword for a container-typed field, assuming it would be immutable.

Though Secure Cadence implements theÂ [Cadence mutability restrictions FLIP](https://github.com/onflow/flips/blob/main/cadence/20211129-cadence-mutability-restrictions.md), it did not fully solve the problem / prevent the foot-gun and there were still ways to mutate such fields, so a proper solution was devised.

To learn more about the problem and motivation to solve it, please read the associatedÂ [Vision](https://github.com/onflow/flips/blob/main/cadence/vision/mutability-restrictions.md)Â document.

#### â„¹ï¸ Description

The mutability of containers (updating a field of a composite value, key of a map, or index of an array) through references has changed:
When a field/element is accessed through a reference, a reference to the accessed inner object is returned, instead of the actual object. These returned references are unauthorized by default, and the author of the object (struct/resource/etc.) can control what operations are permitted on these returned references by using entitlements and entitlement mappings.
This improvement was proposed in two FLIPs:

- [FLIP 89: Change Member Access Semantics](https://github.com/onflow/flips/blob/main/cadence/20230517-member-access-semnatics.md)
- [FLIP 86: Introduce Built-in Mutability EntitlementsÂ 1](https://github.com/onflow/flips/blob/main/cadence/20230519-built-in-mutability-entitlements.md)

To learn more, please consult the FLIPs and the documentation.

#### ğŸ”„ Adoption

As mentioned in the previous section, the most notable change in this improvement is that, when a field/element is accessed through a reference, a reference to the accessed inner object is returned, instead of the actual object. So developers would need to change their code to:

- Work with references, instead of the actual object, when accessing nested objects through a reference.
- Use proper entitlements for fields when they declare their ownÂ `struct`Â andÂ `resource`Â types.

<br />

#### âœ¨ Example

Consider the below resource collection:

```cadence
pub resource MasterCollection {
  pub let kittyCollection: @Collection
  pub let topshotCollection: @Collection
}

pub resource Collection {
  pub(set)
  var id: String

  access(all)
  var ownedNFTs: @{UInt64: NonFungibleToken.NFT}

  access(all)
  fun deposit(token:@NonFungibleToken.NFT) {... }
}
```

Earlier, it was possible to mutate the inner collections, even if someone only had a reference to theÂ `MasterCollection`. e.g:

```cadence
var masterCollectionRef:&MasterCollection =... // Directly updating the field
masterCollectionRef.kittyCollection.id = "NewID"

// Calling a mutating function
masterCollectionRef.kittyCollection.deposit(<-nft)

// Updating via the referencelet ownedNFTsRef=&masterCollectionRef.kittyCollection.ownedNFTs as &{UInt64: NonFungibleToken.NFT}
destroy ownedNFTsRef.insert(key: 1234, <-nft)

```

Once this change is introduced, the above collection can be re-written as below:

```cadence
pub resource MasterCollection {
  access(KittyCollectorMapping)
  let kittyCollection: @Collection

  access(TopshotCollectorMapping)
  let topshotCollection: @Collection
}

pub resource Collection {
  pub(set)
  var id: String

  access(Identity)
  var ownedNFTs: @{UInt64: NonFungibleToken.NFT}

  access(Insert)
  fun deposit(token:@NonFungibleToken.NFT) { /* ... */ }
}

// Entitlements and mappings for `kittyCollection`

entitlement KittyCollector

entitlement mapping KittyCollectorMapping {
  KittyCollector -> Insert
  KittyCollector -> Remove
}

// Entitlements and mappings for `topshotCollection`

entitlement TopshotCollector

entitlement mapping TopshotCollectorMapping {
  TopshotCollector -> Insert
  TopshotCollector -> Remove
}
```

Then for a reference with no entitlements, none of the previously mentioned operations would be allowed:

```cadence
var masterCollectionRef:&MasterCollection <- ... // Error: Cannot update the field. Doesn't have sufficient entitlements.
masterCollectionRef.kittyCollection.id = "NewID"

// Error: Cannot directly update the dictionary. Doesn't have sufficient entitlements.
destroy masterCollectionRef.kittyCollection.ownedNFTs.insert(key: 1234,<-nft)
destroy masterCollectionRef.ownedNFTs.remove(key: 1234)

// Error: Cannot call mutating function. Doesn't have sufficient entitlements.
masterCollectionRef.kittyCollection.deposit(<-nft)

// Error: `masterCollectionRef.kittyCollection.ownedNFTs` is already a non-auth reference.// Thus cannot update the dictionary. Doesn't have sufficient entitlements.
let ownedNFTsRef = &masterCollectionRef.kittyCollection.ownedNFTsas&{UInt64: NonFungibleToken.NFT}
destroy ownedNFTsRef.insert(key: 1234, <-nft)
```

To perform these operations on the reference, one would need to have obtained a reference with proper entitlements:

```cadence
var masterCollectionRef: auth{KittyCollector} &MasterCollection <- ... // Directly updating the field
masterCollectionRef.kittyCollection.id = "NewID"

// Updating the dictionary
destroy masterCollectionRef.kittyCollection.ownedNFTs.insert(key: 1234, <-nft)
destroy masterCollectionRef.kittyCollection.ownedNFTs.remove(key: 1234)

// Calling a mutating function
masterCollectionRef.kittyCollection.deposit(<-nft)
```

</details>

<details>

<summary>Removal Of Nested Type Requirements ([FLIP 118](https://github.com/onflow/flips/blob/main/cadence/20230711-remove-type-requirements.md))</summary>

#### ğŸ’¡ Motivation

[Nested Type RequirementsÂ 3](https://docs.onflow.org/cadence/language/interfaces/#nested-type-requirements)Â were a fairly advanced concept of the language.

Just like an interface could require a conforming type to provide a certain field or function, it could also have required the conforming type to provide a nested type.

This is an uncommon feature in other programming languages and hard to understand.

In addition, the value of nested type requirements was never realized. While it was previously used in the FT and NFT contracts, the addition of other language features like interface inheritance and events being emittable from interfaces, there were no more uses case compelling enough to justify a feature of this complexity.

#### â„¹ï¸ Description

Contract interfaces can no longer declare any concrete types (`struct`,Â `resource`Â orÂ `enum`) in their declarations, as this would create a type requirement.Â `event`Â declarations are still allowed, but these create anÂ `event`Â type limited to the scope of that contract interface; thisÂ `event`Â is not inherited by any implementing contracts. Nested interface declarations are still permitted, however.

This improvement was proposed inÂ [FLIP 118](https://github.com/onflow/flips/blob/main/cadence/20230711-remove-type-requirements.md).

#### ğŸ”„ Adoption

Any existing code that made use of the type requirements feature should be rewritten not to use this feature.

</details>

<details>

<summary>Event Definition And Emission In Interfaces ([FLIP 111](https://github.com/onflow/flips/blob/main/cadence/20230417-events-emitted-from-interfaces.md))</summary>

#### ğŸ’¡ Motivation

In order to support the removal of nested type requirements, events have been made define-able and emit-able from contract interfaces, as events were among the only common uses of the type requirements feature.

#### â„¹ï¸ Description

Contract interfaces may now define event types, and these events can be emitted from function conditions and default implementations in those contract interfaces.

This improvement was proposed inÂ [FLIP 111](https://github.com/onflow/flips/blob/main/cadence/20230417-events-emitted-from-interfaces.md).

#### ğŸ”„ Adoption

Contract interfaces that previously used type requirements to enforce that concrete contracts which implement the interface should also declare a specific event, should instead define and emit that event in the interface.

#### âœ¨ Example

**Before:**

A contract interface like the one below (`SomeInterface`) used a type requirement to enforce that contracts which implement the interface also define a certain event (`Foo`):

```cadence
contract interface SomeInterface {
  event Foo()
//^^^^^^^^^^^ type requirement

  fun inheritedFunction()
}

contract MyContract: SomeInterface {
  event Foo()
//^^^^^^^^^^^ type definition to satisfy type requirement

  fun inheritedFunction() {
//  ...
    emit Foo()
  }
}
```

**After:**

This can be rewritten to emit the event directly from the interface, so that any contracts that implementÂ `Intf`Â will always emitÂ `Foo`Â whenÂ `inheritedFunction`Â is called:

```cadence
contract interface Intf {
  event Foo()
//^^^^^^^^^^^ type definition

  fun inheritedFunction() {
    pre {
      emit Foo()
    }
  }
}
```

</details>

<details>

<summary>Force Destruction of Resources ([FLIP 131](https://github.com/onflow/flips/pull/131))</summary>

#### ğŸ’¡ Motivation

It was previously possible to panic in the body of a resource or attachmentâ€™sÂ `destroy`Â method, effectively preventing the destruction or removal of that resource from an account. This could be used as an attack vector by handing people undesirable resources or hydrating resources to make them extremely large or otherwise contain undesirable content.

#### â„¹ï¸ Description

Contracts may no longer defineÂ `destroy`Â functions on their resources, and are no longer required to explicitly handle the destruction of resource fields. These will instead be implicitly destroyed whenever a resource is destroyed.
Additionally, developers may define aÂ `ResourceDestroyed`Â event in the body of a resource definition using default arguments, which will be lazily evaluated and then emitted whenever a resource of that type is destroyed.
This improvement was proposed inÂ [FLIP 131](https://github.com/onflow/flips/pull/131).

#### ğŸ”„ Adoption

Contracts that previously used destroy methods will need to remove them, and potentially define a ResourceDestroyed event to track destruction if necessary.

#### âœ¨ Example

A pair of resources previously written as:

```cadence
event E(id: Int)

resource SubResource {
  let id: Int
  init(id: Int) {
    self.id = id
  }

  destroy() {
    emit E(id: self.id)
  }
}

resource R {
  let subR: @SubResource

  init(id: Int) {
    self.subR <- create SubResource(id: id)
  }

  destroy() {
    destroy self.subR
  }
}
```

can now be equivalently written as:

```cadence
resource SubResource {
  event ResourceDestroyed(id: Int = self.id)
  let id: Int

  init(id: Int) {
    self.id = id
  }
}

resource R {
  let subR: @SubResource

  init(id: Int) {
    self.subR <- create SubResource(id: id)
  }
}
```

</details>

<details>

<summary>NewÂ `domainSeparationTag`Â parameter added toÂ `Crypto.KeyList.verify`</summary>

#### ğŸ’¡ Motivation

`KeyList`â€™sÂ `verify`Â function used to hardcode the domain separation tag (`"FLOW-V0.0-user"`) used to verify each signature from the list. This forced users to use the same domain tag and didnâ€™t allow them to scope their signatures to specific use-cases and applications. Moreover, theÂ `verify`Â function didnâ€™t mirror theÂ `PublicKey`Â signature verification behaviour which accepts a domain tag parameter.

#### â„¹ï¸ Description

`KeyList`â€™sÂ `verify`Â function requires an extra parameter to specify the domain separation tag used to verify the input signatures. The tag is is a singleÂ `string`Â parameter and is used with all signatures. This mirrors the behaviour of the simple public keyÂ [signature verification](https://cadence-lang.org/docs/1.0/language/crypto#signature-verification).

#### ğŸ”„ Adoption

Contracts that useÂ `KeyList`Â need to update the calls toÂ `verify`Â by adding the new domain separation tag parameter. Using the tag asÂ `"FLOW-V0.0-user"`Â would keep the exact same behaviour as before the breaking change. Applications may also define a new domain tag for their specific use-case and use it when generating valid signatures, for added security against signature replays. Check theÂ [signature verification doc](https://cadence-lang.org/docs/1.0/language/crypto#signature-verification)Â and specificallyÂ [hashing with a tag](https://cadence-lang.org/docs/1.0/language/crypto#hashing-with-a-domain-tag)Â for details on how to generate valid signatures with a tag.

#### âœ¨ Example

A previous call toÂ `KeyList`â€™sÂ `verify`Â is written as:

```cadence
let isValid = keyList.verify(
  signatureSet: signatureSet,
  signedData: signedData
)
```

can now be equivalently written as:

```cadence
let isValid = keyList.verify(
  signatureSet: signatureSet,
  signedData: signedData,
  domainSeparationTag: "FLOW-V0.0-user"
)
```

Instead of the existing hardcoded domain separation tag, a new domain tag can be defined, but it has to be also used when generating valid signatures, e.g.Â `"my_app_custom_domain_tag"`.

</details>

## FT / NFT Standard changes

In addition to the upcoming language changes, the Cadence 1.0 upgrade also includes breaking changes to core contracts - such as the FungibleToken and NonFungibleToken standards. All Fungible & Non-Fungible Token contracts will need to be updated to the new standard.

These interfaces are being upgraded to allow for multiple tokens per contract, fix some issues with the original standards, and introduce other various improvements suggested by the community.

- Original Proposal:Â [http://forum.flow.com/t/streamlined-token-standards-proposal/3075](http://forum.flow.com/t/streamlined-token-standards-proposal/3075)
- Fungible Token Changes PR:Â [WIP: V2 FungibleToken Standard by joshuahannan Â· Pull Request #77 Â· onflow/flow-ft Â· GitHubÂ 5](https://github.com/onflow/flow-ft/pull/77)
- NFT Changes PR:Â [https://github.com/onflow/flow-nft/pull/126Â 8](https://github.com/onflow/flow-nft/pull/126)

It will involve upgrading your token contracts with changes to events, function signatures, resource interface conformances, and other small changes.

There are some existing guides for upgrading your token contracts to the new standard:

- [Upgrading Fungible Token Contracts](./ft-guide.mdx)
- [Upgrading Non-Fungible Token Contracts](./nft-guide.mdx)

## More Resources

If you have any questions or need help with the upgrade, feel free to reach out to the Flow team on the [Flow Discord](https://discord.gg/flowblockchain).

Help is also available during the [Cadence 1.0 Office Hours](https://calendar.google.com/calendar/ical/c_47978f5cd9da636cadc6b8473102b5092c1a865dd010558393ecb7f9fd0c9ad0%40group.calendar.google.com/public/basic.ics) each week at 10:00am PST on the Flow Developer Discord.